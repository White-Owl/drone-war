// This drone is very aggressive. It looks for any other drone, regardless of
// is it friend or foe, runs toward it and shoot.

0 direction store

main_loop:
	direction read look

	// if drones sees a wall, that means it does not see any drones
	isWall not sees_a_drone jumpif
	stop             // do not move if drone does not have a target
	drop2            // if we sees a wall, then drop the distance to it (stack should be empty now)
	0 360 random     // get a random direction value in [0-360] range
	direction store  // and the drone will look for the next target in this random direction
	main_loop jump

sees_a_drone:
	dup direction store   // store the direction to the drone
	dup move              // start moving toward the target
	shoot                 // and shoot in the same direction
	drop                  // ignore the result of shooting

	// after charged to the nearest drone, we still have to cleanup data for
	// other objects seen by look.
look_cleanup:
	swap drop swap drop     // drop direction and distance to the next target
	isWall main_loop jumpif // the last target was a wall
	look_cleanup jump       // repeat clean up process

// user function
// drop two values from the stack
sub drop2 drop drop endsub
